#
unset multiplot

if (strstrt(GPVAL_TERMINALS, 'cairolatex') == 0 ) { print "cairolatex not found. Exit"; exit}

if (GPVAL_VERSION >= 5.0 && strstrt(GPVAL_TERMINALS, 'cairolatex') > 0 ) {
	set style arrow 1 nohead lc rgb 'black' lw 1 dt 2	#arrow
	set style line 1 lt 3 lc rgb 'cyan' lw 1	#grid
	set style line 2 dt 2 lw 2 lc rgb 'black'	#dashed
	set style line 3 dt 1 lw 2 lc rgb 'black'
	set style line 4 dt 1 lw 2 lc rgb 'red'
	set style line 5 dt 1 lw 2 lc rgb 'blue'
	set style line 6 dt 1 lw 2 lc rgb 'web-green'
	set style line 7 dt 1 lw 2 lc rgb 'orange'
	set style line 8 dt 1 lw 2 lc rgb 'dark-magenta'
}

if (GPVAL_VERSION < 5.0 && strstrt(GPVAL_TERMINALS, 'cairolatex') > 0 ) {
	set style arrow 1 nohead lt 2 lc rgb 'black' lw 1	#arrow
	set style line 1 lt 3 lc rgb 'cyan' lw 1	#grid
	set style line 2 lt 2 lw 2 lc rgb 'black'	#dashed
	set style line 3 lt 1 lw 2 lc rgb 'black'
	set style line 4 lt 1 lw 2 lc rgb 'red'
	set style line 5 lt 1 lw 2 lc rgb 'blue'
	set style line 6 lt 1 lw 2 lc rgb 'web-green'
	set style line 7 lt 1 lw 2 lc rgb 'orange'
	set style line 8 lt 1 lw 2 lc rgb 'dark-magenta'
}

colors = 6


# margins
MT = 0.10
MP = (1-2*MT)/2.0

# file information
names = system("head -n1 ".f_all)
col_bot = system("head -n1 ".f_dif." | wc -w") - 1
col_top = system("head -n1 ".f_all." | wc -w")

##
#bottom plot

stats f_dif u 1:2 name 'bot' nooutput
ymin_bot = bot_min_y
ymax_bot = bot_max_y
do for [c=3:col_bot] { 
     stats f_dif using 1:c name 'bot' nooutput
     ymin_bot = (ymin_bot > bot_min_y) ? bot_min_y : ymin_bot
     ymax_bot = (ymax_bot < bot_max_y) ? bot_max_y : ymax_bot
}

##
#top plot

stats f_all u 1:2 name 'top' nooutput
ymin_top = top_min_y
ymax_top = top_max_y
do for [c=3:col_top] { 
     stats f_all using 1:c name 'top' nooutput
     ymin_top = (ymin_top > top_min_y) ? top_min_y : ymin_top
     ymax_top = (ymax_top < top_max_y) ? top_max_y : ymax_top
}

fitp = top_pos_min_y

#set term qt size 750,525
set term cairolatex pdf color size 6in, 4.8in lw 2 dashed #standalone header \
       #"\\usepackage\{amsmath\}"
set output outp

set xr [xr_0:xr_1]

set multiplot
#
## BOT plot

set tmargin 0.0 #at screen 1-MT
set rmargin 0.5 #at screen 1-MT
set lmargin at screen MT
set bmargin at screen 1.5*MT
set origin 0, 0
set size 1, (MT+MP)

set xl x_lab
set xtics xr_0, (xr_1-xr_0)*4/(nn-1), xr_1
set format x x_form
set mxtics 4



#set style line 12 lc rgb 'cyan' lw 1 dt "."
set grid xtics, mxtics ls 1 back

tt = 10.
#extend range by tt% above and below
#ymin_bot = ymin_bot + tt * dy_bot
#ymax_bot = ymax_bot + tt * dy_bot

#print ymax_bot, ymin_bot
if (ymax_bot == ymin_bot) {
	set yr [-0.1:0.1]
	set ytics -0.1, 8, 0.1
	set mytics 4
} else {
	ey_bot = floor(log(ymax_bot - ymin_bot) / log(tt))

	#round up the closest common number
	ymin_bot = floor(ymin_bot / tt**ey_bot) * tt**ey_bot
	ymax_bot = ceil (ymax_bot / tt**ey_bot) * tt**ey_bot
	dy_bot = ymax_bot - ymin_bot

	ntics = dy_bot / tt**ey_bot
	set yr [ymin_bot:ymax_bot]
	set ytics ymin_bot, ntics > 6 ? 2*tt**ey_bot : tt**ey_bot, ymax_bot * 0.9
	set mytics 4
}

set format y "%g"
set ylabel "\$\\Delta \\chi^2_0 - \\Delta \\chi^2\$"

set arrow 1 arrowstyle 1 from getx(fitp), ymin_bot to getx(fitp), ymax_bot
#set key title "Difference" at fitp+0.02*(xr_1-xr_0), ymin_bot + 0.85 * dy_bot top left samplen 1.5
# word(names, i+1)
plot for [i=2:col_bot] f_dif u (getx($1)):i w histe ls (i+2)%colors not, \
	0 w l ls 3 lc rgb 'black' not

#
## TOP plot

set tmargin 0.5 #at screen 1-MT
set rmargin 0.5 #at screen 1-MT
set lmargin at screen MT
set bmargin 0
set origin 0, (MT+MP)
set size 1, (MT+MP)


set xl ""
set format x ""


ntics = (ymax_top - ymin_top) / 10. 
#print "ntics ", ntics
if (ntics < 5.) {
	dd = 5.
} else {
	if (ntics < 10) {
	dd = 10.
} else {
	if (ntics < 20) {
	dd = 20.
} else {
	if (ntics < 50) {
	dd = 50.
} else {
	if (ntics < 100) {
	dd = 100.
} else {
	if (ntics < 500) {
	dd = 500.
} else {
	dd = 1000.
}}}}}}


ey_top = floor(log(ymax_top - ymin_top) / log(tt))

#round up the closest common number
ymin_top = floor(ymin_top / dd) * dd
ymax_top = ceil (ymax_top / dd) * dd
dy_top = ymax_top - ymin_top


ntics = dy_top / dd

set yr [ymin_top:ymax_top]
#print "increment ", ntics > 6 ? 2*dd : dd
set ytics ymin_top, ntics > 6 ? 2*dd : dd, ymax_top
set mytics 4

set format y "%g"
set ylabel "\$\\Delta \\chi^2\$"

set arrow 2 arrowstyle 1 from getx(fitp), ymin_top to getx(fitp), ymax_top
set key title "" at getx(fitp)+0.02*(xr_1-xr_0), ymin_top + 0.75 * (ymax_top - ymin_top) top left samplen 1.5
plot for [i=2:col_top] f_all u (getx($1)):i w histe ls (i+1)%colors t system("echo ".word(names, i)." | sed 's:_: :g'")

       #'valor_nostat.dat' w l lw 2 lc rgb 'green' t "VALOR No syst", \
       #'valor_stat.dat' w l lw 2 lc rgb 'cyan' t "VALOR syst"


unset multiplot
unset output

#system("mv -f ".dataset."*.* ".dataset."/")
